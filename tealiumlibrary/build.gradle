apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-allopen'
apply from: '../jacoco.gradle'

version = '1.5.5'

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        buildConfigField 'String', 'TAG', "\"Tealium-$version\""
        buildConfigField 'String', 'LIBRARY_NAME', "\"android-kotlin\""
        buildConfigField 'String', 'LIBRARY_VERSION', "\"$version\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        pickFirst("META-INF/AL2.0")
        pickFirst("META-INF/LGPL2.1")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'com.tealium:remotecommands:1.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_core_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"
    implementation 'androidx.annotation:annotation:1.3.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation 'junit:junit:4.13'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    testImplementation("com.squareup.okhttp3:mockwebserver:4.3.1")
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_core_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.tealium'
                artifactId = 'kotlin-core'
            }
        }
        repositories {
//            maven {
//                url "s3://maven.tealiumiq.com/android/releases/"
//                credentials(AwsCredentials) {
//                    accessKey AWS_ACCESS_KEY
//                    secretKey AWS_SECRET_KEY
//                    sessionToken AWS_SESSION_TOKEN
//                }
//            }
        }
    }
}

allOpen {
    annotation 'com.tealium.test.OpenClass'
}

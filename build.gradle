// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.8.22'
    ext.coroutines_core_version = '1.8.0'
    ext.coroutines_android_version = '1.8.0'
    ext.coroutines_test_version = '1.8.0'
    ext.mockk_version = '1.13.16'
    ext.robolectric_version = '4.14.1'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jacoco:org.jacoco.core:0.8.7"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"

        // Firebase integration
        classpath 'com.google.gms:google-services:4.4.2'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
}


allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url "https://maven.tealiumiq.com/android/releases/" }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.getLayout().getBuildDirectory()
}

subprojects {
    if (project.name != "mobile") {
        apply plugin: "io.gitlab.arturbosch.detekt"

        detekt {
            toolVersion = "1.19.0"
            config = files("$rootDir/detekt.yml")
            baseline = file("$projectDir/detekt-baseline.xml")
        }

        tasks.named("detekt").configure {
            reports {
                html.required.set(true)
                html.outputLocation.set(file("build/reports/detekt.html"))
            }
        }
    }

    afterEvaluate {
        if (project.extensions.findByName("android") != null) {
            android {
                kotlinOptions.freeCompilerArgs = ["-Xstring-concat=inline"]
                packagingOptions {
                    resources {
                        excludes += "/META-INF/{AL2.0,LGPL2.1}"
                        merges += "META-INF/LICENSE.md"
                        merges += "META-INF/LICENSE-notice.md"
                    }
                }
            }
        }
    }
}